/*
  MIT License

  Copyright (c) 2025 Newport Robotics Group

  Permission is hereby granted, free of charge, to any person obtaining a copy
  of this software and associated documentation files (the "Software"), to deal
  in the Software without restriction, including without limitation the rights
  to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
  copies of the Software, and to permit persons to whom the Software is
  furnished to do so, subject to the following conditions:

  The above copyright notice and this permission notice shall be included in
  all copies or substantial portions of the Software.

  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
  IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
  FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
  AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
  LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
  OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
  SOFTWARE.
*/

plugins {
    // Apply the java-library plugin for API and implementation separation.
    id 'java-library'

    id 'maven-publish'
    id "com.diffplug.spotless"  version "7.0.1"
}

spotless {
    java {
        googleJavaFormat()
        licenseHeaderFile  "./.styleguide-license"
    }
}

group = 'com.nrg948'
version = '2025.3.2' + (Boolean.valueOf(System.getProperty("release")) ? "" : "-SNAPSHOT")

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()

    // Add the WPILib Maven repository
    maven {
        url = 'https://frcmaven.wpi.edu/artifactory/release/'
    }
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.2'

    api "edu.wpi.first.wpilibj:wpilibj-java:2025.3.2"
    api "org.javatuples:javatuples:1.2"

    implementation "edu.wpi.first.cscore:cscore-java:2025.3.2"
    implementation "edu.wpi.first.ntcore:ntcore-java:2025.3.2"
    implementation "edu.wpi.first.wpiutil:wpiutil-java:2025.3.2"
    implementation 'org.reflections:reflections:0.10.2'
    implementation 'edu.wpi.first.wpilibNewCommands:wpilibNewCommands-java:2025.3.2'
    implementation 'org.dom4j:dom4j:2.1.3'
}

java {
    withJavadocJar()
    withSourcesJar()
}

tasks.named('test') {
    // Use JUnit Platform for unit tests.
    useJUnitPlatform()
}

javadoc {
    options {
        links 'https://docs.oracle.com/en/java/javase/17/docs/api/','https://github.wpilib.org/allwpilib/docs/release/java/','https://www.javatuples.org/apidocs/'
    }
}

publishing {
    repositories {
        mavenLocal()
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/NRG948/nrgcommon"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}

// Reformat Java files before compiling the library.
compileJava.dependsOn 'spotlessApply'
